<?xml version="1.0" encoding="utf-8"?>
<s:MXTreeItemRenderer creationComplete="mxtreeitemrenderer1_creationCompleteHandler(event)" dataChange="mxtreeitemrenderer1_dataChangeHandler(event)" xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" xmlns:mx="library://ns.adobe.com/flex/mx">

	<fx:Script>
		<![CDATA[
			import com.estudio.flex.utils.ArrayCollectionUtils;
			import com.estudio.flex.utils.Convert;

			import mx.controls.Tree;
			import mx.events.FlexEvent;

			private var selectedFieldName:String="selected";

			protected function checkbox1_clickHandler(event:MouseEvent):void
			{
				data[selectedFieldName]=!data[selectedFieldName];
				if (data.children)
				{
					var list:Array=[];
					ArrayCollectionUtils.TreeData2List(data.children, list);
					for (var i:int=0; i < list.length; i++)
					{
						list[i][selectedFieldName]=data[selectedFieldName];
					}
					LayerTree(this.owner).invalidateList();
					LayerTree(this.owner).switchDynamicLayerVisible(null, selectedFieldName);
				}
				else
					LayerTree(this.owner).switchDynamicLayerVisible(data, selectedFieldName);

				event.stopPropagation();
				event.stopImmediatePropagation();
			}

			private function getIcon():Class
			{
				if (!data)
					return null;
				if (data.root)
					return rootIcon;
				if (data.type == 0)
					return groupIcon;
				if (data.layerType == 0)
					return pointIcon;
				if (data.layerType == 1)
					return lineIcon;
				if (data.layerType == 2)
					return polygonIcon;
				if (data.layerType == 4)
					return serverIcon;
				return null;
			}

			[Embed(source="/assets/webgis/server16.png")]
			[Bindable]
			public var rootIcon:Class;

			[Embed(source="/assets/webgis/group.png")]
			[Bindable]
			public var groupIcon:Class;

			[Embed(source="/assets/webgis/point16.png")]
			[Bindable]
			public var pointIcon:Class;

			[Embed(source="/assets/webgis/line16.png")]
			[Bindable]
			public var lineIcon:Class;

			[Embed(source="/assets/webgis/polygon16.png")]
			[Bindable]
			public var polygonIcon:Class;

			[Embed(source="/assets/webgis/ServerArcIMS16.png")]
			[Bindable]
			public var serverIcon:Class;

			protected function mxtreeitemrenderer1_dataChangeHandler(event:FlexEvent):void
			{
				img.source=getIcon();
			}

			protected function mxtreeitemrenderer1_creationCompleteHandler(event:FlexEvent):void
			{
				selectedFieldName=Object(this.owner).selectedFieldName;
				if (LayerTree(this.owner).checkEnabled)
					chk.selected=Convert.object2Boolean(data[selectedFieldName]);
				else
					(chk.parent as Object).removeElement(chk);

			}
		]]>
	</fx:Script>


	<s:states>
		<s:State name="normal"/>
		<s:State name="hovered"/>
		<s:State name="selected"/>
	</s:states>
	<s:HGroup left="0" right="0" top="0" bottom="0" verticalAlign="middle">
		<s:Rect id="indentationSpacer" width="{treeListData.indent}" percentHeight="100" alpha="0">
			<s:fill>
				<s:SolidColor color="0xFFFFFF"/>
			</s:fill>
		</s:Rect>
		<s:Group id="disclosureGroup">
			<s:BitmapImage source="{treeListData.disclosureIcon}" visible="{treeListData.hasChildren}"/>
		</s:Group>
		<s:CheckBox width="14" id="chk" verticalCenter="0" selected="{data[selectedFieldName]}" click="checkbox1_clickHandler(event)"/>
		<s:Image width="16" height="16" id="img" verticalCenter="0"/>
		<s:Label fontWeight="{data.root?'bold':'normal'}" id="labelField" text="{treeListData.label}" paddingTop="2"/>
	</s:HGroup>
</s:MXTreeItemRenderer>
