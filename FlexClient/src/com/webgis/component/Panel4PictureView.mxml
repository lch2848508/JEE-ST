<?xml version="1.0" encoding="utf-8"?>
<s:VGroup gap="0" xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="100%"
		  xmlns:component="com.webgis.component.*">
	<fx:Script>
		<![CDATA[
			import com.estudio.flex.utils.AjaxUtils;
			import com.estudio.flex.utils.AlertUtils;
			import com.estudio.flex.utils.ArrayUtils;
			import com.estudio.flex.utils.StringUtils;
			import com.webgis.service.MapServiceClient;

			import flash.net.navigateToURL;

			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.events.Request;

			import spark.events.IndexChangeEvent;

			public var mapServiceClient:MapServiceClient=null;
			[Bindable]
			public var readonly:Boolean=false;
			[Bindable]
			public var recordId:String="";
			[Bindable]
			public var layerId:String="";
			[Bindable]
			public var dataProvider:ArrayCollection=null;

			[Bindable]
			public var pictureTypes:ArrayCollection=null;


			public var callFunction:Function=null;

			protected function btnUpload_clickHandler(event:MouseEvent):void
			{
				WinUploadPicture.execute(mapServiceClient, uploadPictureCallFunction, {id: recordId, layerId: layerId, pictureTypes: pictureTypes, selectedIndex: categoryCombobox.selectedIndex});
			}

			///////////////////////////////////////////////////////////////////////////////////////////
			private function uploadPictureCallFunction(json:Object):void
			{
				dataProvider.addItem(json);
				dataProvider.refresh();
				callFunction(json);
				var selectIndex:int=ArrayUtils.indexOf(pictureTypes.source, json.category);
				if (categoryCombobox.selectedIndex != selectIndex)
				{
					categoryCombobox.selectedIndex=selectIndex;
					categoryCombobox.selectedItem=pictureTypes.getItemAt(selectIndex);
					categoryCombobox_changeHandler(null);
				}
			}

			///////////////////////////////////////////////////////////////////////////////////////////

			protected function btnDelete_clickHandler(event:MouseEvent):void
			{
				var files:Array=[];
				var items:Array=[];
				for (var i:int=0; i < dataProvider.length; i++)
				{
					var item:Object=dataProvider.getItemAt(i);
					if (item.selected)
					{
						files.push(item.url);
						files.push(item.small);
						items.push(item);
					}
				}
				if (files.length != 0)
				{
					AlertUtils.confirm("确定要删除选择的图片？", function():void
					{
						AjaxUtils.postData("../webgis/pictureService?o=delete", {files: JSON.stringify(files)}, null);
						for (var i:int=0; i < items.length; i++)
						{
							dataProvider.removeItemAt(dataProvider.getItemIndex(items[i]));
						}
						imageList.invalidateDisplayList();
						callFunction(null);
					});
				}
			}

			protected function categoryCombobox_creationCompleteHandler(event:FlexEvent):void
			{
				categoryCombobox.textInput.editable=false;
			}

			///////////////////////////////////////////////////////////////////////////////////
			protected function categoryCombobox_changeHandler(event:IndexChangeEvent):void
			{
				dataProvider.filterFunction=function(item:Object):Boolean
				{
					if (categoryCombobox.selectedIndex < 1)
						return true;
					return StringUtils.equal(item.category, categoryCombobox.selectedItem);
				};
				dataProvider.refresh();
				imageList.invalidateDisplayList();
			}

			////////////////////////////////////////////////////////////////////////////////////

			protected function imageList_doubleClickHandler(event:MouseEvent):void
			{
				var selectedItem:Object=imageList.selectedItem;
				if (selectedItem)
				{
					WinPictureView.execute(dataProvider, imageList.selectedItem, (categoryCombobox.selectedIndex >= 0 ? categoryCombobox.selectedItem as String : categoryCombobox.dataProvider.getItemAt(0) as String), function()
					{
						if (!readonly)
							callFunction(null);
					});
				}
			}

			////////////////////////////////////////////////////////////////////////////////////////
			//图片上移
			protected function toolbarbutton1_clickHandler(event:MouseEvent):void
			{
				var index:int=imageList.selectedIndex;
				if (index == 0 || index < 0)
					return;
				var item:Object=dataProvider.getItemAt(index);
				dataProvider.removeItemAt(dataProvider.getItemIndex(item));
				dataProvider.addItemAt(item, index - 1);
				dataProvider.refresh();
				imageList.invalidateDisplayList();
				callFunction(null);
				imageList.callLater(function():void
				{
					imageList.selectedItem=item;
					imageList.ensureIndexIsVisible(index - 1);
				});
			}

			/////////////////////////////////////////////////////////////////////////////////////////
			//图片下移
			protected function toolbarbutton2_clickHandler(event:MouseEvent):void
			{
				var index:int=imageList.selectedIndex;
				if (index == dataProvider.length - 1 || index < 0)
					return;
				var item:Object=dataProvider.getItemAt(index);
				dataProvider.removeItemAt(dataProvider.getItemIndex(item));
				dataProvider.addItemAt(item, index + 1);
				dataProvider.refresh();
				imageList.invalidateDisplayList();
				callFunction(null);
				imageList.callLater(function():void
				{
					imageList.selectedItem=item;
					imageList.ensureIndexIsVisible(index + 1);
				});
			}
			/////////////////////////////////////////////////////////////////////////////////////////////
		]]>
	</fx:Script>
	<s:HGroup gap="2" paddingLeft="2" paddingRight="2" height="28" width="100%" verticalAlign="middle">
		<s:Label text="类别"/>
		<s:ComboBox width="100%" selectedIndex="0" change="categoryCombobox_changeHandler(event)" id="categoryCombobox" dataProvider="{pictureTypes}"
					creationComplete="categoryCombobox_creationCompleteHandler(event)"/>
		<component:ToolbarButton enabled="{!readonly}" click="btnUpload_clickHandler(event)" selected="true" id="btnUpload" toolTip="上传图片" icon="@Embed('/assets/webgis/upload.png')"/>
		<component:ToolbarButton enabled="{!readonly}" click="toolbarbutton1_clickHandler(event)" selected="true" toolTip="向上移动" icon="@Embed('/assets/webgis/up.png')"/>
		<component:ToolbarButton enabled="{!readonly}" click="toolbarbutton2_clickHandler(event)" selected="true" toolTip="向下移动" icon="@Embed('/assets/webgis/down.png')"/>
		<component:ToolbarButton enabled="{!readonly}" click="btnDelete_clickHandler(event)" selected="true" id="btnDelete" toolTip="删除选择的图片" icon="@Embed('/assets/common/delete.png')"/>
	</s:HGroup>

	<s:Line width="100%" height="0">
		<s:stroke>
			<s:SolidColorStroke color="#DDE1E5"/>
		</s:stroke>
	</s:Line>

	<s:List selectionColor="0xFFFF00" id="imageList" doubleClickEnabled="true" doubleClick="imageList_doubleClickHandler(event)" width="100%" height="100%"
			itemRenderer="com.webgis.component.ListItemRender4PictureView" dataProvider="{dataProvider}">

	</s:List>
</s:VGroup>
