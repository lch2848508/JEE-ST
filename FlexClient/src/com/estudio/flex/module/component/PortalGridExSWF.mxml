<?xml version="1.0" encoding="utf-8"?>
<component:PortalGridExControl creationComplete="portalgridexcontrol1_creationCompleteHandler(event)" xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" xmlns:mx="library://ns.adobe.com/flex/mx"
							   xmlns:component="com.estudio.flex.module.component.*" width="100%" height="100%">
	<fx:Script>
		<![CDATA[
			import com.estudio.flex.common.InterfaceFormUI;
			import com.estudio.flex.utils.UIUtils;

			import mx.controls.SWFLoader;
			import mx.events.FlexEvent;
			import mx.events.ModuleEvent;
			import mx.managers.SystemManager;

			import spark.modules.ModuleLoader;
			private var swfInstance:Object=null;
			private var executeFunctionCalls:Array=[];
			public var isSWFLoaded:Boolean=false;

			override public function initParams(controlParams:Object):void
			{
				super.initParams(controlParams);
				var url:String=controlParams.url;
				if (controlParams.isModule)
				{
					var module:ModuleLoader=new ModuleLoader();
					UIUtils.fullAlign(module);
					module.addEventListener(ModuleEvent.READY, function(event:ModuleEvent):void
					{
						swfInstance=(event.currentTarget as ModuleLoader).child;
						for (var i:int=0; i < executeFunctionCalls.length; i++)
						{
							executeFunction(executeFunctionCalls[i][0], executeFunctionCalls[i][1]);
						}
						executeFunctionCalls=[];
						isSWFLoaded=true;
					});
					this.addElement(module);
					module.loadModule(url);
				}
				else
				{
					var swf:SWFLoader=new SWFLoader();
					UIUtils.fullAlign(swf);
					swf.addEventListener(Event.COMPLETE, function(event:Event):void
					{
						var loaderSWF:SWFLoader=event.target as SWFLoader;
						var obj:SystemManager=loaderSWF.content as SystemManager;
						obj.addEventListener(FlexEvent.APPLICATION_COMPLETE, function(event:FlexEvent)
						{
							swfInstance=event.target.application;
							for (var i:int=0; i < executeFunctionCalls.length; i++)
							{
								executeFunction(executeFunctionCalls[i][0], executeFunctionCalls[i][1]);
							}
							executeFunctionCalls=[];
							isSWFLoaded=true;
						});
					});
					this.addElement(swf);
					swf.load(url);
				}
			}

			//////////////////////////////////////////////////////////////////////
			public function executeFunction(funName:String, params:Object):Object
			{
				if (swfInstance && swfInstance.hasOwnProperty(funName))
					return swfInstance[funName](params);
				else if (!swfInstance)
					executeFunctionCalls.push([funName, params]);
				return null;
			}

			/////////////////////////////////////////////////////////////////////
			protected function portalgridexcontrol1_creationCompleteHandler(event:FlexEvent):void
			{
				_isCreateCompleted=true;
			}
			//////////////////////////////////////////////////////////////////////
		]]>
	</fx:Script>
</component:PortalGridExControl>
