<?xml version="1.0" encoding="utf-8"?>
<component:TitleWindowEx fontSize="12" title="车辆安排座位" creationComplete="titlewindowex1_creationCompleteHandler(event)" close="titlewindowex1_closeHandler(event)" xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" xmlns:mx="library://ns.adobe.com/flex/mx"
						 xmlns:component="com.estudio.flex.component.*" width="640" height="480" xmlns:swf="ext.swf.*">
	<fx:Script>
		<![CDATA[
			import com.estudio.flex.utils.AjaxUtils;
			import com.estudio.flex.utils.AlertUtils;
			import com.estudio.flex.utils.Convert;

			import mx.collections.ArrayCollection;
			import mx.core.FlexGlobals;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;

			import spark.events.IndexChangeEvent;
			//---------------------------------------------------------------------------------------------
			private var planitem_id:String;
			private var _order_id:String;
			private var type:int;
			private var busSeatInfo:Object=null;
			private var _seatInfoIsChanged:Boolean=false;

			public function get seatInfoIsChanged():Boolean
			{
				return _seatInfoIsChanged;
			}

			public function set seatInfoIsChanged(value:Boolean):void
			{
				_seatInfoIsChanged=value;
			}


			public function get order_id():String
			{
				return _order_id;
			}

			public function set order_id(value:String):void
			{
				_order_id=value;
			}


			//---------------------------------------------------------------------------------------------
			//关闭窗体
			protected function btn_Close_clickHandler(event:MouseEvent):void
			{
				FlexGlobals.topLevelApplication.closePopupWindow(this);

			}

			//----------------------------------------------------------------------------------------------
			private function drawSeatInfo():void
			{
				if (order_id != "0")
				{
					gridUserList.dataProvider=new ArrayCollection(busSeatInfo.users);
					gridUserList.invalidateDisplayList();
					if (gridUserList.dataProvider.length != 0)
					{
						gridUserList.selectedIndex=0;
						gridUserList.selectedItem=ArrayCollection(gridUserList.dataProvider).getItemAt(0);
					}
				}

				comboboxBusList.dataProvider=new ArrayCollection(busSeatInfo.bus);
				if (comboboxBusList.dataProvider.length != 0)
				{
					comboboxBusList.selectedIndex=0;
					comboboxBusList.selectedItem=comboboxBusList.dataProvider.getItemAt(0);
				}
				comboboxBusList_changeHandler(null);

				comboboxBusList.callLater(function()
				{
					gridUserList_doubleClickHandler(null);
				});
			}

			//----------------------------------------------------------------------------------------------
			//关闭按钮
			protected function titlewindowex1_closeHandler(event:CloseEvent):void
			{
				btn_Close_clickHandler(null);
			}

			//----------------------------------------------------------------------------------------------
			private function loadBusSeatInfo():void
			{
				_seatInfoIsChanged=false;
				AjaxUtils.postData("../ext/tour_utils.jsp", {o: "arrangeseatinfo", planitem_id: planitem_id, order_id: order_id}, function(text, token:Object):void
				{
					instance.busSeatInfo=JSON.parse(text);
					if (instance.busSeatInfo.r && instance.busSeatInfo.bus)
						instance.drawSeatInfo();
					else
					{
						instance.btn_Close_clickHandler(null);
						AlertUtils.alert("没有车辆信息", AlertUtils.ALERT_WARNING);
					}
				});
			}

			//----------------------------------------------------------------------------------------------
			protected function titlewindowex1_creationCompleteHandler(event:FlexEvent):void
			{
				comboboxBusList.textInput.editable=false;
				busSeatTable.win=this;
				gridUserList.setStyle("alternatingItemColors", [0xFFFFFF, 0xFFFFFF]);
				loadBusSeatInfo();
			}

			//----------------------------------------------------------------------------------------------
			private static var instance:WinArrangSeatInfo=new WinArrangSeatInfo();

			//执行
			public static function execute(planitem_id:String, order_id:String, type:int):void
			{
				instance.planitem_id=planitem_id;
				instance.order_id=order_id;
				instance.type=type;
				if (instance.busSeatTable)
				{
					instance.loadBusSeatInfo();
				}
				FlexGlobals.topLevelApplication.showPopupWindow(instance, true);

				//PopUpManager.centerPopUp(instance);
			}


			protected function comboboxBusList_changeHandler(event:IndexChangeEvent):void
			{
				var selectedItem:Object=comboboxBusList.selectedItem;
				if (selectedItem)
				{
					busSeatTable.seatNumber=selectedItem.seat_number;
					var userList:Array=selectedItem.seats;
					busSeatTable.seatUserInfos=userList;
					if (order_id != "0")
					{
						var records:ArrayCollection=gridUserList.dataProvider as ArrayCollection;
						for (var i=0; i < records.length; i++)
						{
							var record:Object=records.getItemAt(i);
							if (record.busid == comboboxBusList.selectedItem.id)
							{
								busSeatTable.orderSeat(record.seat_no);
							}
						}
					}
					else //显示座位
					{
						var userRecords:Array=new Array();
						for (var i=0; i < selectedItem.seats.length; i++)
						{
							if (selectedItem.seats[i].tourist_id)
							{
								var record:Object={};
								record.id=selectedItem.seats[i].tourist_id;
								record.name=selectedItem.seats[i].name;
								record.busname=selectedItem.name;
								record.seat_no=selectedItem.seats[i].seat_no;
								record.busid=selectedItem.id;
								userRecords.push(record);
							}
						}
						gridUserList.dataProvider=new ArrayCollection(userRecords);
						gridUserList.invalidateDisplayList();
					}
				}
				else if (order_id == "0")
				{
					gridUserList.dataProvider=new ArrayCollection([]);
					gridUserList.invalidateDisplayList();
				}
			}

			////////////////////////////////////////////////////////////////////////////////////////////////
			protected function btn_Auto_clickHandler(event:MouseEvent):void
			{
				var seatCount:int=0;
				var records:ArrayCollection=ArrayCollection(gridUserList.dataProvider);
				var needRecords:Array=[];
				for (var i=0; i < records.length; i++)
				{
					if (Convert.str2int(records.getItemAt(i).busid, 0) == 0)
					{
						seatCount++;
						needRecords.push(records.getItemAt(i));
					}
				}
				if (seatCount != 0)
				{
					var seats:Array=busSeatTable.autoArrangeSeatInfo(seatCount);
					for (var j:int=0; j < seats.length; j++)
					{
						needRecords[j].busname=comboboxBusList.selectedItem.name;
						needRecords[j].busid=comboboxBusList.selectedItem.id;
						needRecords[j].seat_no=seats[j].seat_no;
					}
					gridUserList.dataProvider=records;
					gridUserList.invalidateDisplayList();
				}
			}

			/////////////////////////////////////////////////////////////////////////////////////////////////
			//排座位 
			protected function btn_OK_clickHandler(event:MouseEvent):void
			{
				if (this.order_id == "0" && !_seatInfoIsChanged)
					return btn_Close_clickHandler(null);
				var records:ArrayCollection=ArrayCollection(gridUserList.dataProvider);
				var uids:Array=[];
				var bids:Array=[];
				var seats:Array=[];

				for (var i:int=0; i < records.length; i++)
				{
					var record:Object=records.getItemAt(i);
					uids.push(record.id);
					bids.push(record.busid);
					seats.push(record.seat_no);
				}

				AjaxUtils.postData("../ext/tour_utils.jsp", {o: "save", planitem_id: planitem_id, order_id: order_id, uids: JSON.stringify(uids), bids: JSON.stringify(bids), seats: JSON.stringify(seats)}, function(text, token:Object):void
				{
					var json:Object=JSON.parse(text);
					if (json.r)
					{
						btn_Close_clickHandler(null);
					}
					else
					{
						AlertUtils.alert("部分座位未能排好，原因可能其他客户已经占用!");
						instance.loadBusSeatInfo();
					}
				});

			}

			//----------------------------------------------------------------------------------------------
			protected function gridUserList_doubleClickHandler(event:MouseEvent):void
			{
				var selectedItem:Object=gridUserList.selectedItem;
				if (selectedItem && comboboxBusList.selectedItem)
				{
					if (Convert.str2int(selectedItem.busid, 0) != 0)
					{
						for (var i=0; i < comboboxBusList.dataProvider.length; i++)
						{
							if (comboboxBusList.dataProvider.getItemAt(i).id * 1 == selectedItem.busid * 1 && selectedItem.busid * 1 != comboboxBusList.selectedItem.id * 1)
							{
								comboboxBusList.selectedItem=comboboxBusList.dataProvider.getItemAt(i);
								comboboxBusList_changeHandler(null);
								break;
							}
						}
					}
				}
			}
		]]>
	</fx:Script>

	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<s:VGroup width="100%" height="100%" paddingTop="2" paddingLeft="2" paddingRight="2" gap="0">

		<s:HGroup width="100%" height="100%" gap="4">
			<s:VGroup width="249" height="100%" id="groupleft">
				<s:HGroup width="100%" verticalAlign="middle" paddingTop="4" paddingBottom="2" paddingLeft="4" gap="2" paddingRight="2">
					<s:Label text="车辆列表" fontWeight="bold">
					</s:Label>
					<s:ComboBox width="100%" id="comboboxBusList" labelField="name" change="comboboxBusList_changeHandler(event)">
					</s:ComboBox>
				</s:HGroup>
				<mx:DataGrid width="100%" height="100%" headerHeight="28" rowHeight="28" id="gridUserList" doubleClickEnabled="true" doubleClick="gridUserList_doubleClickHandler(event)">
					<mx:columns>
						<mx:DataGridColumn dataField="name" headerText="姓名">
						</mx:DataGridColumn>
						<mx:DataGridColumn dataField="busname" headerText="车号">
						</mx:DataGridColumn>
						<mx:DataGridColumn dataField="seat_no" headerText="座位">
						</mx:DataGridColumn>
					</mx:columns>
				</mx:DataGrid>
			</s:VGroup>
			<s:VGroup width="100%" height="100%" gap="0">
				<s:Label width="100%" height="25" fontSize="14" fontWeight="bold" verticalAlign="middle" text="选中用户后双击座位号选择(取消选择)座位！">

				</s:Label>
				<s:BorderContainer width="100%" height="100%" borderVisible="false">
					<swf:BusSeatTable width="100%" height="100%" id="busSeatTable">
					</swf:BusSeatTable>
				</s:BorderContainer>

			</s:VGroup>
		</s:HGroup>
		<s:Group width="100%" height="40">
			<s:Button id="btn_Auto" left="5" width="75" height="30" label="自动排位" click="btn_Auto_clickHandler(event)" verticalCenter="0">

			</s:Button>
			<s:Button id="btn_Close" right="5" width="75" height="30" label="关闭" click="btn_Close_clickHandler(event)" verticalCenter="0">

			</s:Button>

			<s:Button id="btn_OK" right="85" width="75" height="30" label="确定" verticalCenter="0" click="btn_OK_clickHandler(event)">

			</s:Button>
		</s:Group>
	</s:VGroup>
</component:TitleWindowEx>
